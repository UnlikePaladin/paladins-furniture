plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/pfm.accesswidener")

	runs {
		datagen {
			server()

			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			runDir "build/datagen"
		}
	}
}

repositories {
	maven { url "https://maven.shedaniel.me" }
	maven { url 'https://maven.blamejared.com' }
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name 'Gegy'
		url 'https://maven.gegy.dev'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = uri("https://storage.googleapis.com/devan-maven/")
	}
	mavenLocal()
}

if (project.sandwichable_compatibility == "true") {
	dependencies {
		modImplementation("maven.modrinth:sandwichable:${project.sandwichable_version}")
		modImplementation("dev.lambdaurora:spruceui:${project.spruceui_version}")
	}
}
else {
	dependencies {
		modCompileOnly("maven.modrinth:sandwichable:${project.sandwichable_version}")
		modCompileOnly("dev.lambdaurora:spruceui:${project.spruceui_version}")
	}
}

if (project.rei_compatibility == "true"){
	dependencies {
		modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
		modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	}
}
else {
	dependencies {
		modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	}
}

if (project.patchouli_compatibility == "true") {
	dependencies {
		modImplementation("vazkii.patchouli:Patchouli:${project.patchouli_version}")
	}
}
else {
	dependencies {
		modCompileOnly("vazkii.patchouli:Patchouli:${project.patchouli_version}")
	}
}

if (project.emi_compatibility == "true"){
	dependencies {
		modImplementation "dev.emi:emi:${project.emi_version}"
	}
}
else {
	dependencies {
		modCompileOnly "dev.emi:emi:${project.emi_version}"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.devtech:arrp:${project.arrp_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 17
}

assemble.dependsOn runDatagen

sourceSets {
	main {
		resources {
			srcDirs += [
					"src/main/generated"
			]
		}
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}



jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}




// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}